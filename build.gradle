apply plugin: 'javacard'

group 'org.gitian.javacard'
version '1.0-SNAPSHOT'

buildscript {
  repositories {
    maven { url 'http://releases.marmeladburk.fidesmo.com/' }
    mavenCentral()
    maven { url 'https://jitpack.io' }
  }

  dependencies {
    classpath 'com.fidesmo:gradle-javacard:0.2.7'
    classpath 'com.github.status-im.status-keycard-java:desktop:4a69788'
  }
}

javacard {
  sdkVersion = "3.0.4"

  cap {
    aid = '0x0F:0x00:0xBA:0x00:0x00:0x00:0x01'
    packageName = 'org.gitian.javacard'
    applet {
      aid = '0x0F:0x00:0xBA:0x00:0x00:0x00:0x01:0x01'
      className = 'HelloWorldApplet'
    }
    version = '0.1'
  }
}

def testTarget = project.properties['im.status.keycard.test.target']
if (!testTarget) {
  testTarget = 'card'
}

def pairingPass = project.properties['im.status.keycard.test.pairing']
if (!pairingPass) {
  pairingPass = 'KeycardTest'
}


repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

dependencies {
  testCompile(files("../jcardsim/jcardsim-3.0.5-SNAPSHOT.jar"))
  testCompile('com.github.status-im.status-keycard-java:desktop:4a69788')
  testCompile("org.junit.jupiter:junit-jupiter-api:5.1.1")
  testRuntime("org.junit.jupiter:junit-jupiter-engine:5.1.1")
}

test {
  classpath = sourceSets.test.runtimeClasspath.filter { f -> f.name != "api_classic.jar" }
  useJUnitPlatform {
    excludeTags 'manual'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.7'
}

task install (type: org.gitian.javacard.build.InstallTask)

if (testTarget == 'card') {
  tasks.install.dependsOn(convertJavacard)
  tasks.test.dependsOn(install)
}

compileJava {
  sourceCompatibility = 1.6
  targetCompatibility = 1.6
}

compileTestJava {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

afterEvaluate {
  def jvmArgs = ["-Dim.status.keycard.test.target=${testTarget}", "-Dim.status.keycard.test.pairing=${pairingPass}"]

  if (testTarget == 'simulator') {
    jvmArgs.push('-noverify')
  }

  def testTask = tasks.getByName('test')
  testTask.jvmArgs(jvmArgs)
}
