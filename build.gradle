import org.gitian.javacard.build.InstallTask
import org.gitian.javacard.build.PingTask

apply plugin: 'javacard'

group 'org.gitian.javacard'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        maven { url 'http://releases.marmeladburk.fidesmo.com/' }
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath 'com.fidesmo:gradle-javacard:0.2.7'
    }
}

javacard {
    sdkVersion = "3.0.4"

    cap {
        aid = '0x0F:0x00:0xBA:0x00:0x00:0x00:0x01'
        packageName = 'org.gitian.javacard'
        applet {
            aid = '0x0F:0x00:0xBA:0x00:0x00:0x00:0x01:0x01'
            className = 'HelloWorldApplet'
        }
        version = '0.1'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    testCompile(files("../jcardsim-status/jcardsim-3.0.4-SNAPSHOT.jar"))
    testCompile("org.junit.jupiter:junit-jupiter-api:5.1.1")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.1.1")
}

test {
    classpath = sourceSets.test.runtimeClasspath.filter { f -> f.name != "api_classic.jar" }
    useJUnitPlatform {
        excludeTags 'manual'
    }
}

task install(type: InstallTask)
task uninstall(type: InstallTask) {
    skipInstall = true
}
tasks.install.dependsOn tasks.convertJavacard
tasks.uninstall.dependsOn tasks.convertJavacard

task ping(type: PingTask)

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}
